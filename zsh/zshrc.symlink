export LC_CTYPE='en_US.UTF-8'

# Vagrant path
VAGRANT_PATH='~/vagrant'

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U +X compinit && compinit

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH=/usr/local/share/npm/bin:/Applications/sdk/tools:$HOME/.rvm/bin:/usr/local/bin:$PATH # Add RVM to PATH for scripting
